apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
// make sure this line comes *after* you apply the Android plugin
apply plugin: 'com.getkeepsafe.dexcount'

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

def android_support_version = '27.0.0'

version = VERSION_NAME

configurations {
    javadocDeps
}

dependencies {
    implementation 'com.android.support:support-annotations:' + android_support_version
    implementation 'com.android.support:appcompat-v7:' + android_support_version
    // Api for this import because we use reflection to alter TextInputLayout
    api 'com.android.support:design:' + android_support_version

    javadocDeps 'com.android.support:support-annotations:' + android_support_version
    javadocDeps 'com.android.support:appcompat-v7:' + android_support_version
    javadocDeps 'com.android.support:design:' + android_support_version
    compileOnly 'javax.annotation:jsr250-api:1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    testImplementation 'org.robolectric:robolectric:3.5.1'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
}

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        consumerProguardFiles 'proguard-rules.txt'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {

        }
    }
    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
        }
    }
    productFlavors {
    }
}

apply from: 'deploy.gradle'
